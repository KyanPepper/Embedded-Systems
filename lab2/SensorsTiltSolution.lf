target C {
  platform: {
    name: "rp2040",
    board: "pololu_3pi_2040_robot"
  },
  single-threaded: true
}

import Accelerometer from "lib/IMU.lf"
import Display from "lib/Display.lf"
import Tilt from "lib/Tilt.lf"

main reactor {

  a = new Accelerometer()
  t = new Tilt()
  d = new Display()

  // fires at t=0 then every 100 ms
  timer sample(0, 100 msec)

  reaction(sample) -> a.trigger {=
    lf_set(a.trigger, true);
  =}

  a.x -> t.x
  a.y -> t.y
  a.z -> t.z

  reaction(t.pitch, t.roll, a.x, a.y) -> d.line0, d.line1, d.line2, d.line3 {=
    // 16 chars + null terminator
    static char buf0[17];
    static char buf1[17];
    static char buf2[17];
    static char buf3[17];

    snprintf(buf0, 17, "P:%+7.2f deg", t.pitch->value);
    snprintf(buf1, 17, "R:%+7.2f deg", t.roll->value);
    snprintf(buf2, 17, "x:%+.4f g", a.x->value);
    snprintf(buf3, 17, "y:%+.4f g", a.y->value);

    lf_set(d.line0, buf0);
    lf_set(d.line1, buf1);
    lf_set(d.line2, buf2);
    lf_set(d.line3, buf3);
  =}
}
